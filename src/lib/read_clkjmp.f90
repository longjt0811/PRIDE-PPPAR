!
!! read_clkjmp.f90
!!
!!    Copyright (C) 2023 by Wuhan University
!!
!!    This program belongs to PRIDE PPP-AR which is an open source software:
!!    you can redistribute it and/or modify it under the terms of the GNU
!!    General Public License (version 3) as published by the Free Software Foundation.
!!
!!    This program is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
!!    GNU General Public License (version 3) for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with this program. If not, see <https://www.gnu.org/licenses/>.
!!
!! Contributor: Yuanxin Pan, Jianghui Geng, Songfeng Yang, Jihang Lin
!!
!!
!!
!!! purpose  :  read receiver clock jump file generated by tedit
!!
!! parameter:       jd:    modified julian day
!!                 sod:    second of day
!!              lfnjmp:    clock jump file unit
!!                nprn:    number of used satellite prn
!!                  OB:    struct of RINEX-OBS
!
subroutine read_clkjmp(jd, sod, lfnjmp, nprn, OB)
  implicit none
  include "../header/const.h"
  include "../header/rnxobs.h"

! common
  integer*4 idxfrq(MAXSYS, 2)
  common idxfrq
! parameter
  type(rnxobr) OB
  integer jd, lfnjmp, nprn
  real*8 sod
! local
  real*8, save :: jumpvals(2) = 0.d0
  character*256 buf
  real*8 t, jump
  real*8 f1(MAXSYS), f2(MAXSYS)
  integer i0, i, flag ! 1:range, 2:phase
  integer*4 frequency_glo_nu, prn_int
  real*8 FREQ1_R(-50:50), FREQ2_R(-50:50)

!
!! frequency
  call frequency_glonass(FREQ1_R, FREQ2_R)
  do i0 = 1, MAXSYS
    f1(i0) = FREQ_SYS(idxfrq(i0, 1), i0)
    if (f1(i0) .eq. 0.d0) goto 100
    f2(i0) = FREQ_SYS(idxfrq(i0, 2), i0)
    if (f2(i0) .eq. 0.d0) goto 100
  end do
!
!! read file
  read (lfnjmp, '(a)', err=777, end=777) buf
  read (buf, *) t
  if ((sod - t) .lt. 0.d0) then
    backspace (lfnjmp)
  else
    read (buf, *) t, flag, jump
    jumpvals(flag) = jumpvals(flag) + jump
  end if

777 continue
  do i = 1, nprn
    if (dabs(OB%obs(i, 1)) .lt. 1.d-3) cycle
    i0 = index(GNSS_PRIO, OB%prn(i) (1:1))
    if (OB%prn(i) (1:1) .eq. 'R') then
      read (OB%prn(i), '(1x,i2)') prn_int
      frequency_glo_nu = OB%glschn(prn_int)
      f1(i0) = freq1_R(frequency_glo_nu)
      f2(i0) = freq2_R(frequency_glo_nu)
    end if
    OB%obs(i, 1) = OB%obs(i, 1) + jumpvals(2)/vlight*f1(i0)
    OB%obs(i, 2) = OB%obs(i, 2) + jumpvals(2)/vlight*f2(i0)
    OB%obs(i, 3) = OB%obs(i, 3) + jumpvals(1)
    OB%obs(i, 4) = OB%obs(i, 4) + jumpvals(1)
  end do
  return

100 continue
  write (*, '(a,5(1x,a,2i1))') '***ERROR(lsq_add_obs): invalid frequency number:', &
    'G', idxfrq(index(GNSS_PRIO, 'G'), 1:2), &
    'R', idxfrq(index(GNSS_PRIO, 'R'), 1:2), &
    'E', idxfrq(index(GNSS_PRIO, 'E'), 1:2), &
    'C', idxfrq(index(GNSS_PRIO, 'C'), 1:2), &
    'J', idxfrq(index(GNSS_PRIO, 'J'), 1:2)
  call exit(1)
end subroutine
